buildscript {
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:1.2.4'
        classpath 'com.moowork.gradle:gradle-grunt-plugin:0.10'
	    classpath 'se.inera.intyg:gradle-fitnesse-plugin:1.0-SNAPSHOT'
    }
}

apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'com.moowork.grunt'
apply plugin: 'idea'

sourceSets {
    integrationTest {
        java {
            compileClasspath += sourceSets.main.output + sourceSets.test.output
            runtimeClasspath += sourceSets.main.output + sourceSets.test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    systemProperties['integration.tests.baseUrl'] =  'http://localhost:8790/'

    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

idea {
    module {
        //and some extra test source dirs
        scopes.TEST.plus += [ configurations.integrationTestCompile, configurations.integrationTestRuntime ]
    }
}

grunt {
    colors = true
}

node {
    version = '0.12.4'
    download = true
    distBaseUrl = 'https://build-inera.nordicmedtest.se/node/'
}

if (project.hasProperty('rehabstod.useMinifiedJavaScript')) {
    war {
        webAppDirName = 'build/webapp'
    }
    war.dependsOn grunt_build
}
else {
    war.dependsOn grunt_build_no_minify
}


task assembleManifestClasspath() << {
    war {
        manifest {
            def buildNumber = System.env.BUILD_NUMBER != null ? System.env.BUILD_NUMBER : '1.0.0-SNAPSHOT'
            attributes("Build": buildNumber)
            def gitRevision = System.env.GIT_COMMIT != null ? System.env.GIT_COMMIT : 'Local-build'
            attributes("Git-revision" : gitRevision)
            attributes("Build-Time": new Date())
        }
    }
}

war.dependsOn assembleManifestClasspath

task createVersionPropertyFile(dependsOn: processResources) << {
    def propertyFile = "${buildDir}/resources/main/version.properties"
    ant.touch(file: propertyFile, mkdirs: "true")
    ant.propertyfile(file: propertyFile) {
        entry(key: 'buildVersion', default: buildVersion)
        entry(key: 'buildTime', default: buildTime)
        entry(key: 'buildNumber', default: buildNumber)
    }
}

createVersionPropertyFile.dependsOn assembleManifestClasspath

war.dependsOn createVersionPropertyFile

task bowerInstall(type: NodeTask, dependsOn:npmInstall) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('src/main/webapp/bower_components')
}

grunt_build.dependsOn bowerInstall
grunt_build_no_minify.dependsOn bowerInstall

gretty {
    httpPort = 8790
    contextConfigFile = "${projectDir}/src/main/resources/jetty-web.xml"
    contextPath = '/'
    logbackConfigFile = "${projectDir}/src/main/resources/logback.xml"
    jvmArgs = [
        '-Dspring.profiles.active=dev,rhs-it-stub',
        '-Drehabstod.config.file=' + projectDir + '/src/main/resources/dev.properties',
        '-Dcredentials.file=' + projectDir + '/src/main/resources/dev-credentials.properties',
        '-Drehabstod.resources.folder=/' +projectDir + '/../src/main/resources',
        '-XX:MaxPermSize=256M',
        '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=8011'
    ]

/***************** Konfiguration för att köra med SITHS och HSA aktiverat lokalt. *******************/
//    jvmArgs = [
//            '-Dspring.profiles.active=dev,rhs-security-test',
//            '-Drehabstod.config.folder=' + projectDir + '/../../rehabstod-konfiguration/demo',
//            '-Drehabstod.config.file=' + projectDir + '/../../rehabstod-konfiguration/demo/rehabstod.properties',
//            '-Dcredentials.file=' + projectDir + '/../../rehabstod-konfiguration/demo/credentials.properties',
//            '-Drehabstod.resources.folder=/' +projectDir + '/../src/main/resources'
//    ]
    fastReload = true
    inplaceMode = 'hard'
    scanInterval = 0
    enableNaming = true
}

dependencies {
    compile project(":rehabstod-common")
    compile project(":rehabstod-it-integration")

    compile "se.inera.intyg.common:hsa-integration:0-SNAPSHOT"
    compile "se.inera.intyg.common:log-messages:0-SNAPSHOT"


    
    compile "org.springframework:spring-webmvc:${springContextVersion}"
    compile "org.springframework:spring-jms:${springContextVersion}"
    compile "org.springframework.ws:spring-ws-core:2.2.2.RELEASE"
    compile "org.springframework.security:spring-security-config:${springSecurityVersion}"
    compile "org.springframework.security.extensions:spring-security-saml2-core:${springSecurityExtensionsVersion}"
    compile "org.springframework.security:spring-security-web:${springSecurityVersion}"
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.4'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.4.4'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'org.slf4j:slf4j-api:1.7.10'
    compile 'ch.qos.logback:logback-classic:1.0.13'
    compile 'wsdl4j:wsdl4j:1.6.3'
    compile 'commons-lang:commons-lang:2.6'
    compile 'org.apache.commons:commons-lang3:3.1'
    
    compile 'se.riv.itintegration.monitoring:itintegration-monitoring-schemas:1.0.0.0'

    compile 'org.apache.cxf:cxf-rt-frontend-jaxws:${cxfVersion}'
    compile 'org.apache.cxf:cxf-rt-transports-http:${cxfVersion}'

    compile 'org.apache.poi:poi:3.13'
    compile 'org.apache.poi:poi-ooxml:3.13'
    compile "com.itextpdf:itextpdf:${itextVersion}"


    integrationTestCompile 'com.jayway.restassured:rest-assured:2.8.0'
    integrationTestCompile 'com.jayway.restassured:json-schema-validator:2.8.0'

    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile ('junit:junit:4.12') {
        exclude module : 'hamcrest'
        exclude module : 'hamcrest-core'
    }
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile "org.springframework:spring-test:${springContextVersion}"
    testCompile 'org.unitils:unitils-core:3.3'

    providedCompile "javax.jms:jms-api:1.1-rev-1"
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    providedCompile 'jstl:jstl:1.2'

    gretty 'org.apache.activemq:activemq-client:5.9.1'
    gretty 'org.apache.activemq:activemq-broker:5.9.1'
}